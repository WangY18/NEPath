cmake_minimum_required(VERSION 3.15)
project(NEPath_Demos LANGUAGES CXX)

# Build configuration
if(NOT CMAKE_BUILD_TYPE AND NOT CMAKE_CONFIGURATION_TYPES)
    set(CMAKE_BUILD_TYPE Release CACHE STRING "Choose the type of build." FORCE)
endif()
set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_EXPORT_COMPILE_COMMANDS ON)
message(STATUS "============= Build Configuration =============")
message(STATUS "Build Type: ${CMAKE_BUILD_TYPE}")
message(STATUS "C++ Standard: C++${CMAKE_CXX_STANDARD}")

# Options to enable optional dependencies
option(IncludeIpopt "Enable IPOPT support" ON) # IPOPT: ON / OFF
option(IncludeGurobi "Enable Gurobi support" ON) # Gurobi: ON / OFF

# Compiler definitions: IPOPT
if(IncludeIpopt)
    message(STATUS "Enable IPOPT support")
    add_compile_definitions(IncludeIpopt=1)

    find_package(PkgConfig) # Apply PkgConfig to find IPOPT
    if(PkgConfig_FOUND)
        pkg_check_modules(IPOPT ipopt)
    endif()

    if(DEFINED IPOPT_PKG_FOUND AND IPOPT_PKG_FOUND)
        message(STATUS "Found IPOPT via pkg-config")
    else()
        # Fallback to environment variable or user-provided path
        if(NOT DEFINED IPOPT_ROOT)
            if(DEFINED ENV{IPOPT_ROOT})
                message(STATUS "Using IPOPT_ROOT from environment: $ENV{IPOPT_ROOT}")
                set(IPOPT_ROOT "$ENV{IPOPT_ROOT}")
            else()
                message(FATAL_ERROR "IncludeIpopt=ON but IPOPT not found. Please set IPOPT_ROOT environment variable or pass -DIPOPT_ROOT=/path/to/ipopt")
            endif()
        endif()
        set(IPOPT_INCLUDE_DIRS "${IPOPT_ROOT}/include")
        set(IPOPT_LIBRARY_DIRS "${IPOPT_ROOT}/lib")
        set(IPOPT_LIBRARIES ipopt)
    endif()
    
    set(IPOPT_INCLUDE_DIRS ${IPOPT_INCLUDE_DIRS})
    set(IPOPT_LIBRARY_DIRS ${IPOPT_LIBRARY_DIRS})
    set(IPOPT_LIBRARIES ${IPOPT_LIBRARIES})
    message(STATUS "IPOPT Include Dirs: ${IPOPT_INCLUDE_DIRS}")
    message(STATUS "IPOPT Library Dirs: ${IPOPT_LIBRARY_DIRS}")
    message(STATUS "IPOPT Libraries: ${IPOPT_LIBRARIES}")
    include_directories("${IPOPT_INCLUDE_DIRS}/coin")
    link_directories("${IPOPT_LIBRARY_DIRS}/coin")
else()
    message(STATUS "Disable IPOPT support")
    add_compile_definitions(IncludeIpopt=0)
endif()

# Compiler definitions: Gurobi
if(IncludeGurobi)
    message(STATUS "Enable Gurobi support")
    add_compile_definitions(IncludeGurobi=1)

    find_package(PkgConfig) # Apply PkgConfig to find Gurobi
    if(PkgConfig_FOUND)
        pkg_check_modules(Gurobi gurobi)
    endif()

    if(DEFINED GUROBI_PKG_FOUND AND GUROBI_PKG_FOUND)
        message(STATUS "Found Gurobi via pkg-config")
    else()
        # Fallback to environment variable or user-provided path
        if(NOT DEFINED GUROBI_HOME)
            if(DEFINED ENV{GUROBI_HOME})
                message(STATUS "Using GUROBI_HOME from environment: $ENV{GUROBI_HOME}")
                set(GUROBI_HOME "$ENV{GUROBI_HOME}")
            else()
                message(FATAL_ERROR "IncludeGurobi=ON but Gurobi not found. Please set GUROBI_HOME environment variable or pass -DGUROBI_HOME=/path/to/gurobi")
            endif()
        endif()
        set(GUROBI_INCLUDE_DIRS "${GUROBI_HOME}/include")
        set(GUROBI_LIBRARY_DIRS "${GUROBI_HOME}/lib")

        if(DEFINED GUROBI_VERSION)
            # First use the version defined in CMake
            message(STATUS "Using GUROBI_VERSION from CMake: ${GUROBI_VERSION}")
            set(GUROBI_VERSION "${GUROBI_VERSION}")
        elseif(DEFINED ENV{GUROBI_VERSION})
            # If not defined, use the version from environment variable
            set(GUROBI_VERSION "$ENV{GUROBI_VERSION}")
            message(STATUS "Using GUROBI_VERSION from environment: ${GUROBI_VERSION}")
        else()
            message(FATAL_ERROR "GUROBI_VERSION not defined. Please set GUROBI_VERSION environment variable or pass -DGUROBI_VERSION=x (e.g., if you are using Gurobi 13.0, set GUROBI_VERSION=130)")
        endif()

        set(GUROBI_LIBRARIES gurobi_c++ gurobi${GUROBI_VERSION})
    endif()
    
    set(GUROBI_INCLUDE_DIRS ${GUROBI_INCLUDE_DIRS})
    set(GUROBI_LIBRARY_DIRS ${GUROBI_LIBRARY_DIRS})
    set(GUROBI_LIBRARIES ${GUROBI_LIBRARIES})
    message(STATUS "GUROBI Include Dirs: ${GUROBI_INCLUDE_DIRS}")
    message(STATUS "GUROBI Library Dirs: ${GUROBI_LIBRARY_DIRS}")
    message(STATUS "GUROBI Libraries: ${GUROBI_LIBRARIES}")
    include_directories("${GUROBI_INCLUDE_DIRS}")
    link_directories("${GUROBI_LIBRARY_DIRS}")
else()
    message(STATUS "Disable Gurobi support")
    add_compile_definitions(IncludeGurobi=0)
endif()

# Compiler flags
if(CMAKE_CXX_COMPILER_ID MATCHES "Clang|GNU")
    add_compile_options(
        -Wall
        -Wno-register           # Allow deprecated 'register' keyword (if any remain)
        -Wno-deprecated-declarations
        -Wno-infinite-recursion # Suppress FileAgent::mkdir warning
        -Wno-null-conversion    # Suppress path.cpp NULL conversion warning
        -Wno-pragma-once-outside-header  # Suppress path.cpp pragma once warning
        -Wno-sign-compare      # Suppress sign-compare warnings in NEPath sources
    )
elseif(MSVC)
    add_compile_options(
        /W4
        /wd4996  # Disable deprecation warnings
    )
endif()

# Include directories
include_directories(
    ${CMAKE_SOURCE_DIR}/include
    ${CMAKE_SOURCE_DIR}/src
    ${CMAKE_SOURCE_DIR}/external/clipper
)

# Collect NEPath source files
set(NEPATH_SOURCES
    src/NEPath/Basic.cpp
    src/NEPath/Connector.cpp
    src/NEPath/ContourParallel.cpp
    src/NEPath/Curve.cpp
    src/NEPath/DirectionParallel.cpp
    src/NEPath/FileAgent.cpp
    src/NEPath/NEPathPlanner.cpp
    src/NEPath/PlanningOptions.cpp
    src/NEPath/path.cpp
    external/clipper/clipper.cpp
    src/NEPath/NonEquidistant.cpp
)

# Eliminate warnings from Clipper library
set_source_files_properties(external/clipper/clipper.cpp PROPERTIES
    COMPILE_FLAGS "-w"
)

set(DEMO_LIST
    demo_Raster
    demo_Zigzag
    demo_CP
    demo_CP_CFS
    demo_CP_DFS
    demo_tool_compensate
    demo_underfill
    demo_sharpcorner
    demo_IQOP_Ipopt
    demo_IQOP_Ipopt_CFS
    demo_IQOP_Ipopt_DFS
    demo_IQOP_gurobi
    demo_IQOP_gurobi_CFS
    demo_IQOP_gurobi_DFS
)

foreach(demo ${DEMO_LIST})
    message(STATUS "Building demo: ${demo}")
    add_executable(${demo}
        examples/${demo}.cpp
        ${NEPATH_SOURCES}
    ) # Create test_demos executable
    if(IncludeIpopt)
        target_link_directories(${demo} PRIVATE
            ${IPOPT_LIBRARY_DIRS}
        ) # Add IPOPT link directories
        target_link_libraries(${demo}
            ${IPOPT_LIBRARIES}
            ${CMAKE_DL_LIBS}
        ) # Link IPOPT libraries
    endif()
    if(IncludeGurobi)
        target_link_directories(${demo} PRIVATE
            ${GUROBI_LIBRARY_DIRS}
        ) # Add Gurobi link directories
        target_link_libraries(${demo}
            ${GUROBI_LIBRARIES}
        ) # Link Gurobi libraries
    endif()
endforeach()

message(STATUS "=======================================")